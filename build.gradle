// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        protocVersion = '3.18.0'
        kotlin_version = '1.6.20-M1'
    }
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.4"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

allprojects {
    ext.addFrameworkJar = { String path ->
        def frameworkJar = new File(rootProject.projectDir, 'framework-prebuilt/' + path)
        if (!frameworkJar.exists()) {
            throw new IllegalArgumentException("Framework jar path doesn't exist")
        }
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.bootstrapClasspath = files([frameworkJar] + (options.bootstrapClasspath.files as Iterable<File>))
                options.compilerArgs.add('-Xbootclasspath/p:framework-prebuilt/framework-all.jar')
            }
            tasks.withType(org.jetbrains.kotlin.gradle.internal.KaptWithoutKotlincTask) {
                it.classpath.from(files([frameworkJar] + (it.classpath.files as Iterable<File>)))
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        }
    }
}